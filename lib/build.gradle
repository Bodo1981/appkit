apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

// This is the library version used when deploying the artifact
version = "1.0.0"

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdk
    targetSdkVersion rootProject.ext.targetSdk
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  compile 'com.android.support:appcompat-v7:' + rootProject.ext.appcompat7Version
  compile 'com.android.support:recyclerview-v7:' + rootProject.ext.recyclerviewVersion

  // Icepick
  compile 'frankiesardo:icepick:' + rootProject.ext.icepickVersion

  // Butterknife
  compile 'com.jakewharton:butterknife:' + rootProject.ext.butterknifeVersion

  // PagerSlidingTabStrip
  compile 'com.jpardogo.materialtabstrip:library:' + rootProject.ext.materialtabstripVersion

  // SmoothProgressBar
  compile 'com.github.castorflex.smoothprogressbar:library-circular:' + rootProject.ext.smoothprogressbarVersion

  // FragmentArgs
  compile 'com.hannesdorfmann.fragmentargs:annotation:' + rootProject.ext.fragmentargsVersion

  // Material Design Colors
  compile 'com.wada811:android-material-design-colors:' + rootProject.ext.materialdesigncolorVersion
}

// Homepage URL of the library
def siteUrl = 'https://github.com/Bodo1981/appkit'
// Git repository URL
def gitUrl = 'https://github.com/Bodo1981/appkit.git'

group = "com.christianbahl.appkit"

install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'aar'

        // Add your description here
        name 'AppKit'
        url siteUrl

        // Set your license
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id '<your user ID>'
            name 'Christian Bahl'
            email 'christian.bahl@gmail.com'
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
artifacts {
  archives javadocJar
  archives sourcesJar
}
